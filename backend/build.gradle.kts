import org.springframework.boot.gradle.tasks.bundling.BootBuildImage

plugins {
    java
    id("org.springframework.boot") version "2.7.18"
    id("io.spring.dependency-management") version "1.0.15.RELEASE"
    id("org.springdoc.openapi-gradle-plugin") version "1.8.0"
}

group = "com.bankaccount"
version = "0.0.1-SNAPSHOT"
java.sourceCompatibility = JavaVersion.VERSION_17

configurations {
    compileOnly {
        extendsFrom(configurations.annotationProcessor.get())
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("org.springframework.boot:spring-boot-starter-mail")
    implementation("org.springdoc:springdoc-openapi-ui:1.7.0")
    implementation("org.springdoc:springdoc-openapi-javadoc:1.7.0")
    implementation("com.auth0:java-jwt:4.4.0")

    runtimeOnly("com.h2database:h2")
    runtimeOnly("org.postgresql:postgresql")

    compileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.springframework.security:spring-security-test")
    testImplementation("org.awaitility:awaitility")
}

tasks.named<BootBuildImage>("bootBuildImage") {
    environment = mapOf("BP_JVM_VERSION" to "17")
    isPublish = true
    docker {
        publishRegistry {
            username = "diegoj4v"
            password = project.property("DOCKER_PASS").toString()
            tags = listOf(
                    "diegoj4v/bank-backend:latest",
                    "diegoj4v/bank-backend:${project.property("DOCKER_TAG").toString()}"
            )
        }
    }
}

tasks.withType<Test> {
    useJUnitPlatform()
}

tasks.javadoc {
    options {
        this as StandardJavadocDocletOptions
        addStringOption("Xdoclint:none", "-quiet")
    }
    source = sourceSets.main.get().allJava
    classpath = sourceSets.main.get().compileClasspath
}